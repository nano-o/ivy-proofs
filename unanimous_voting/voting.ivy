#lang ivy1.8

include order
include udp
include collections
include array_set

type value
instance node : iterable

isolate interface(n:node) = {
    export action propose(v:value)
    import action decide(v:value)
    specification {
        relation decided(V:value)
        after init {
            decided(V) := false
        }
        before decide {
            require interface.decided(N,V) -> V = v;
            decided(v) := true;
        }
        invariant interface.decided(N1,V1) & interface.decided(N2,V2) -> V1 = V2
    }
}

isolate abstract_protocol = {
    relation vote(N:node, V:value)
    relation decided(N:node, V:value)
    after init {
        vote(N,V) := false;
        decided(N,V) := false;
    }
    action do_vote(n:node,v:value) = {
        require ~vote(n,V);
        vote(n,v) := true;
    }
    action decide(n:node, v:value) = {
        require vote(N,v);
        decided(n,v) := true
    }
    invariant vote(N,V1) & vote(N,V2) -> V1 = V2
    invariant decided(N,V) -> vote(N,V)
    invariant decided(N1,V1) & decided(N2,V2) -> V1 = V2
}

type msg = struct {
    sender : node,
    val : value
}

instance net : udp_simple(node, msg)

instance node_set : array_set(node)

import action debug_received_vote(self:node, frm:node, v:value)
import action debug_set(self:node, s:node_set)

isolate protocol = {

    invariant interface.decided(N,V) = abstract_protocol.decided(N,V)
    invariant (server(N).voted & V = server(N).candidate) = abstract_protocol.vote(N,V)

    # if somebody's in my local voted-for-candidate set, then i've voted
    invariant node_set.member(N2, server(N).voted_for_candidate) -> server(N).voted
    # if somebody's in my local voted-for-candidate set, then they voted for my candidate
    invariant node_set.member(N2, server(N).voted_for_candidate)
        -> server(N2).voted & server(N2).candidate = server(N).candidate
    # inflight messages correspond to sender state (the sender's vote)
    invariant net.spec.sent(M, DST)
        -> server(sender(M)).voted & server(sender(M)).candidate = val(M)

    # protocol state relates to interface state
    invariant interface.decided(N,V) -> server(N).voted & V = server(N).candidate

    object server(self:node) = {

        function voted: bool
        function candidate: value # first value a node votes for
        function voted_for_candidate: node_set.t

        after init {
            voted := false;
            var empty := node_set.emptyset();
            voted_for_candidate := empty;
        }

        implement interface.propose(v:value) {
            if ~voted { # if we have not voted yet
                call do_vote(v);
            };
        }

        action do_vote(v:value) = {
            call abstract_protocol.do_vote(self,v);
            candidate := v; # remember that we are trying to decide v
            voted_for_candidate := node_set.add(voted_for_candidate, self);
            # broadcast a vote message:
            var m:msg;
            m.sender := self;
            m.val := v;
            var it := node.iter.begin();
            while ~it.is_end
            invariant node.iter.done(N, it) -> net.spec.sent(m, N)
            invariant net.spec.sent(M,N) & ~(old net.spec.sent(M,N)) -> M = m
            # invariant (old net.spec.sent(M,N)) -> net.spec.sent(M,N)
            {
                call net.send(self, it.val, m);
                it := it.next();
            };
            voted := true; # remember that we voted
        }

        implement net.recv(m:msg) {
            # debug_received_vote(self, m.sender, m.val);
            # if not voted yet, vote for the value in the message:
            if ~voted {
                call do_vote(m.val);
            };
            if candidate = m.val {
                voted_for_candidate := node_set.add(voted_for_candidate, m.sender);
            };
            # call debug_set(self, voted_for_candidate);
            if voted_for_candidate.is_universal() {
                call abstract_protocol.decide(self, candidate);
                call interface.decide(self, candidate);
            };
        }
    }
} with interface, net, node, node_set, abstract_protocol

interpret value -> bv[1]

extract impl(n:node) = protocol.server(n), net(n), node, node_set
